      SUBROUTINE FCONST(FX,FP,FM,FF,AA,NABC,IFORCE)
      IMPLICIT REAL*8 (A-H,O-Z)
cets030991
#include <libparse/error.h>
      integer ip, prcntr
      DIMENSION FX(NABC,NABC),FP(NABC,NABC),FM(NABC,NABC)
      DIMENSION FF(NABC,NABC),AA(NABC,NABC)
      COMMON/VIB101/NATOM,NAT1,N3N,ILIN,NVIB,IDIPOL,IPOLAR
      COMMON/VIB106/NSTORE,NPLUS
      COMMON/VIB203/ENGX(50),ENGY(50),ENGZ(50)
      DATA A00,TWO / 0.0D+00 , 2.0D+00 /
      DATA HE,A0 / 4.359813653D+00 , 0.52917706D+00 /
      DATA ITAP15 / 15 /
    1 FORMAT(F10.7)
    2 FORMAT(2I5)
    3 FORMAT(3F20.10)
    4 FORMAT(20X,3F20.14)
    5 FORMAT(//,2X,' FP MATRIX'/)
    6 FORMAT(//,2X,' FM MATRIX'/)
    7 FORMAT(//,2X,' FX MATRIX (IN A.U.)'/)
    8 FORMAT(//,2X,' AVERAGED FX MATRIX (UPPER TRI. = FX, LOWER TRI. = D
     1IFFERENCE)'/)
    9 FORMAT(3F20.10)
   10 FORMAT(//,2X,' FX MATRIX (IN HARTREE/BOHR+2)'/)
   11 FORMAT(//,2X,' FX MATRIX (IN MDYN/A)'/)
C
C   FUNIT IS IN MDYN/A
      FUNIT=HE/(A0*A0)
C
      GO TO (301,302,303),IFORCE
C
C   READ IN ANALYTICAL GRADIENT SETS
C***INPUT DELX***
  301 CONTINUE
      DO 101 I=1,N3N
      DO 101 J=1,N3N
      FP(I,J)=A00
      FM(I,J)=A00
  101 CONTINUE
      READ(5,1) DELX
      REWIND ITAP15
      READ(ITAP15,2) NNATOM,NNSTOR
      DO 104 ICASE=1,NPLUS
      IF(NSTORE.EQ.0) GO TO 201
      READ(ITAP15,2) JCASE
  201 CONTINUE
      IF(ICASE.GT.N3N) GO TO 205
      II=ICASE
      DO 102 J=1,NATOM
      JX=3*(J-1)+1
      JY=JX+1
      JZ=JX+2
      IF(ICASE.GT.NSTORE) GO TO 202
      READ(ITAP15,3) FP(II,JX),FP(II,JY),FP(II,JZ)
      GO TO 102
  202 CONTINUE
      FP(II,JX)=ENGX(J)
      FP(II,JY)=ENGY(J)
      FP(II,JZ)=ENGZ(J)
  102 CONTINUE
      IF(ICASE.LE.N3N) GO TO 104
  205 II=ICASE-N3N
      DO 103 J=1,NATOM
      JX=3*(J-1)+1
      JY=JX+1
      JZ=JX+2
      IF(ICASE.GT.NSTORE) GO TO 206
      READ(ITAP15,3) FM(II,JX),FM(II,JY),FM(II,JZ)
      GO TO 103
  206 CONTINUE
      FM(II,JX)=ENGX(J)
      FM(II,JY)=ENGY(J)
      FM(II,JZ)=ENGZ(J)
  103 CONTINUE
  104 CONTINUE
cets030991
      ip=prcntr('IS_ON BRIEF')
      if(ip.eq.0) then
        WRITE(6,5)
        CALL MATOUT(FP,NABC,NABC,N3N,N3N,6)
        WRITE(6,6)
        CALL MATOUT(FM,NABC,NABC,N3N,N3N,6)
      endif
      IF(NPLUS.LT.2*N3N) GO TO 210
C
C   CALCULATE FORCE CONSTANT MATRIX
      FACT=DELX*TWO
      IF(NPLUS.EQ.N3N) FACT=DELX
      DO 105 I=1,N3N
      DO 105 J=1,N3N
      AA(I,J)=(FP(I,J)-FM(I,J))/FACT
  105 CONTINUE
cets030991
      ip=prcntr('IS_ON BRIEF')
      if(ip.eq.0) then
        WRITE(6,7)
        CALL MATOUT(AA,NABC,NABC,N3N,N3N,6)
      endif
      DO 106 I=1,N3N
      DO 106 J=I,N3N
      FF(I,J)=(AA(I,J)+AA(J,I))/TWO
      IF(I.EQ.J) GO TO 106
      FF(J,I)=(AA(I,J)-AA(J,I))/TWO
  106 CONTINUE
cets030991
      ip=prcntr('IS_ON BRIEF')
      if(ip.eq.0) then
        WRITE(6,8)
        CALL MATOUT(FF,NABC,NABC,N3N,N3N,6)
      endif
      DO 107 I=1,N3N
      DO 107 J=I,N3N
      FX(I,J)=FF(I,J)
      IF(I.EQ.J) GO TO 107
      FX(J,I)=FF(I,J)
  107 CONTINUE
C
C   STORE GRADIENT SETS IN TAPE15
  210 CONTINUE
      REWIND ITAP15
      WRITE(ITAP15,2) NATOM,NPLUS
      DO 110 ICASE=1,NPLUS
      WRITE(ITAP15,2) ICASE
      IF(ICASE.GT.N3N) GO TO 215
      II=ICASE
      DO 108 J=1,NATOM
      JX=3*(J-1)+1
      JY=JX+1
      JZ=JX+2
      WRITE(ITAP15,3) FP(II,JX),FP(II,JY),FP(II,JZ)
  108 CONTINUE
      IF(ICASE.LE.N3N) GO TO 110
  215 CONTINUE
      II=ICASE-N3N
      DO 109 J=1,NATOM
      JX=3*(J-1)+1
      JY=JX+1
      JZ=JX+2
      WRITE(ITAP15,3) FM(II,JX),FM(II,JY),FM(II,JZ)
  109 CONTINUE
  110 CONTINUE
      REWIND ITAP15
      IF(NPLUS.GE.2*N3N) GO TO 305
      RETURN
C
C   READ IN FORCE CONSTANTS FROM TAPE5
  302 CONTINUE
      READ(5,9) ((FX(I,J),J=1,N3N),I=1,N3N)
      GO TO 305
C
C   READ IN ANALYTICAL SECOND DERIVATIVES
  303 CONTINUE
      REWIND ITAP15
      READ(ITAP15,2) NNATOM,NNSTOR
      READ(ITAP15,9) ((FX(I,J),J=1,N3N),I=1,N3N)
      REWIND ITAP15
C
  305 CONTINUE
cets030991
      ip=prcntr('IS_ON BRIEF')
      if(ip.eq.0) then
        WRITE(6,10)
        CALL MATOUT(FX,NABC,NABC,N3N,N3N,6)
      endif
      DO 115 I=1,N3N
      DO 115 J=1,N3N
      FX(I,J)=FX(I,J)*FUNIT
  115 CONTINUE
      WRITE(6,11)
      CALL MATOUT(FX,NABC,NABC,N3N,N3N,6)
C
      RETURN
      END
