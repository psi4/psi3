c_*BEGIN_FILE vibfx.F
      SUBROUTINE VIBFX(FX,ELX,ELXM,EE,VALU,FV1,FV2,FXM,NABC,NPQR)
C   THE NORMAL COORDINATE ANALYSIS FOR CARTESIAN COORDINATE SYSTEM
      IMPLICIT REAL*8 (A-H,O-Z)
cets030991
#include <libparse/error.h>
      integer ip, prcntr
      CHARACTER*80 LABEL
      DIMENSION FX(NABC,NABC),ELX(NABC,NABC),ELXM(NABC,NABC)
      DIMENSION EE(NABC,NABC),VALU(NABC),FV1(NABC),FV2(NABC),FXM(NPQR)
      COMMON/VIB101/NATOM,NAT1,N3N,ILIN,NVIB,IDIPOL,IPOLAR
      COMMON/VIB108/IZVLIM,IPLOT
      COMMON/VIB201/CHG(50),X(50),Y(50),Z(50),W(50)
      COMMON/VIB205/IOFF(150)
      COMMON/VIB206/SQM(150),ROOT(150),FREQ(150)
      DATA LABEL / ' ATOMIC NUMBER , GEOMETRY , FREQUENCIES , AND EIGENV
     1ECTORS FROM NORMCO        ' /
    1 FORMAT(//,2X,' THE NORMAL COORDINATE ANALYSIS BY FX MATRIX METHOD'
     1)
    2 FORMAT(//,2X,' SECULAR EQUATIONS'/2X,' FXM MATRIX'/)
    3 FORMAT(//,3X,23(1H*)/2X,' VIBRATIONAL FREQUENCIES'/3X,23(1H*))
    4 FORMAT(//,2X,' LX MATRIX'/)
    5 FORMAT(//,2X,' LX X LX(T) = M(-1)'/)
    6 FORMAT(//,3X,22(1H*)/2X,' VIBRATIONAL AMPLITUDES'/3X,22(1H*))
    7 FORMAT(//,3X,20(1H*)/2X,' INFRARED INTENSITIES'/3X,20(1H*))
    8 FORMAT(//,3X,17(1H*)/2X,' RAMAN INTENSITIES'/3X,17(1H*))
    9 FORMAT(A80)
   10 FORMAT(5I5)
   11 FORMAT(4F20.10)
   12 FORMAT(F20.10)
   13 FORMAT(3F20.10)
C
      WRITE(6,1)
C   THE FORMATION OF THE SECULAR EQUATION
      DO 101 I=1,NATOM
  101 SQM(I)=DSQRT(W(I))
      DO 102 I=1,N3N
      II=I/3
      IF(I.GT.3*II) II=II+1
      DO 102 J=I,N3N
      JJ=J/3
      IF(J.GT.3*JJ) JJ=JJ+1
      IJ=I+IOFF(J)
      FXM(IJ)=(1./SQM(II))*FX(I,J)*(1./SQM(JJ))
  102 CONTINUE
cets030991
      ip=prcntr('IS_ON BRIEF')
      if(ip.eq.0) then
        WRITE(6,2)
        CALL PRINT(FXM,NPQR,N3N,6)
      endif
C
C   THE NORMAL COORDINATE ANALYSIS
      WRITE(6,3)
      CALL NORMFX(ELXM,EE,VALU,FV1,FV2,FXM,NABC,NPQR)
C
C   THE CALCULATION OF LX MATRIX
      DO 103 I=1,N3N
      II=I/3
      IF(I.GT.3*II) II=II+1
      DO 103 J=1,N3N
  103 ELX(I,J)=(1./SQM(II))*ELXM(I,J)
      WRITE(6,4)
      CALL FRQOUT(ELX,FREQ,NABC,NABC,N3N,N3N,6)
      CALL MTXMPY(ELX,NABC,ELX,NABC,EE,NABC,EE,NABC,N3N,3)
cets030991
      ip=prcntr('IS_ON BRIEF')
      if(ip.eq.0) then
        WRITE(6,5)
        CALL MATOUT(EE,NABC,NABC,N3N,N3N,6)
      endif
C
C   THE CALCULATION OF VIBRATIONAL AMPLITUDES
      WRITE(6,6)
      CALL AMPFX(ELX,NABC)
C
C   THE CALCULATION OF INFRARED INTENSITIES
      IF(IDIPOL.EQ.0) GO TO 201
      WRITE(6,7)
      CALL IRINTX(ELX,NABC)
C
C   THE CALCULATION OF RAMAN INTENSITIES
  201 IF(IPOLAR.EQ.0) GO TO 202
      WRITE(6,8)
      CALL RAMANX(ELX,NABC)
C
C   FORM FILE14 IF NECESSARY
  202 CONTINUE
      IF(IPLOT.EQ.0) GO TO 203
      ITAP14=14
cets  open(unit=14,file='file14',status='unknown')
      call ffile(itap14,' ',0)
      REWIND ITAP14
      WRITE(ITAP14,9) LABEL
      WRITE(ITAP14,10) NATOM,NVIB
      DO 105 I=1,NATOM
      WRITE(ITAP14,11) CHG(I),X(I),Y(I),Z(I)
  105 CONTINUE
      DO 106 IVIB=1,NVIB
      WRITE(ITAP14,12) FREQ(IVIB)
      WRITE(ITAP14,13) (ELXM(I,IVIB),I=1,N3N)
  106 CONTINUE
      DO 107 IVIB=1,NVIB
      WRITE(ITAP14,12) FREQ(IVIB)
      WRITE(ITAP14,13) (ELX(I,IVIB),I=1,N3N)
  107 CONTINUE
      REWIND ITAP14
C
  203 CONTINUE
      RETURN
      END
